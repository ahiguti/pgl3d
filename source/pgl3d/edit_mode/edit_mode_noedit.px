private threaded namespace pgl3d::edit_mode::edit_mode_noedit "use-unsafe";
private import common -;
private import meta m;
private import pgl3d::edit_mode::common -;
private import pgl3d::edit_mode::edit_mode_texture -;
private import pgl3d::edit_mode::sync_gl -;

public struct edit_mode_noedit(ptr{edit_mode_texture} const& emt0)
<edit_mode_i>
{
  // edit_modeが無効のときのedit_mode_i実装。view無しでeditを直接glの
  // テクスチャに反映させる。
  private ptr{edit_mode_texture} emt = emt0;
    // emt->voxがtiled_voxel_texture
    // emt->vox.tmap_editとemt->vox.tpat_editがtptr{texture_edit}
    // emt->glt_voxtmapなどがptr{gl_texture}
  private tptr{texture_edit} tmap_edit = emt->vox.tmap_edit;
  private tptr{texture_edit} tpat_edit = emt->vox.tpat_edit;
  private metafunction num_words multiword_num_words;
  private varray{ptr{gl_texture}} glt_tmap;
  private varray{ptr{gl_texture}} glt_tpat;
  init();
  private function void init()
  {
    glt_tmap.push_back(emt->glt_voxtmap);
    glt_tmap.push_back(emt->glt_voxtmax);
    glt_tpat.push_back(emt->glt_voxtpat);
    glt_tpat.push_back(emt->glt_voxtpax);
  }
  public function void on_textinput(string const& s, string mutable& msg_r)
  {
  }
  public function bool is_textinput_mode() const
  {
    return false;
  }
  public function void on_key_pressed(int scancode, uint modmask,
    glm::vec3 mutable& cursor_move_r, string mutable& msg_r)
  {
  }
  public function void set_camera_angle(glm::quat const& angle)
  {
  }
  public function void on_frame(projection_info const& prj)
  {
    // debug_log("noedit on_frame");
    sync_gl();
    // debug_log("noedit on_frame done");
  }
  public function void modify_tmap(glm::uvec3 const& pos, uint value)
  {
    // TODO: 範囲を一括して更新する手段を用意
    tmap_edit->set(pos, 0, value);
    // debug_log("noedit modify_tmap");
  }
  private function void sync_gl()
  {
    mutable& tme = *tmap_edit;
    mutable& tpe = *tpat_edit;
    /* tmap変更箇所のglへの反映。個々の範囲ごとにコピーする */
    const tmmodarr = tme.get_modified_array(true);
    for (size_t i, const& pos: tmmodarr) {
      const sz = pos.second - pos.first + make_ivec3(1, 1, 1);
      const s = sz.x * sz.y * sz.z;
      // debug_log("noedit sync pos", pos, s);
      texture_data_mip_sync_gl(tme.data[0][0], tme.data[0],
        to_uvec3(pos.first), to_uvec3(sz), glt_tmap[0]);
      texture_data_mip_sync_gl(tme.data[1][0], tme.data[1],
        to_uvec3(pos.first), to_uvec3(sz), glt_tmap[1]);
    }
    pair{uvec3, uvec3} tpmod = tpe.get_modified_merged(true);
    if (tpmod.second.x != 0u) {
      // debug_log("noedit sync tpmod", tpmod);
      texture_data_mip_sync_gl(tpe.data[0][0], tpe.data[0],
        tpmod.first, tpmod.second, glt_tpat[0]);
      texture_data_mip_sync_gl(tpe.data[1][0], tpe.data[1],
        tpmod.first, tpmod.second, glt_tpat[1]);
    }
  }
}

