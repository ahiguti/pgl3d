private threaded namespace pgl3d::edit_mode::texutil "use-unsafe";
public import common -;
public import meta m;
public import glm -;
private import pgl3d::texture::voxel -;
private import pgl3d::texture::editable_voxel -;
private import pgl3d::engine::util -;
private import pgl3d::edit_mode::vecutil -;

private metafunction dbg_rotate{f} no_operation;
private metafunction dbg_scale{f} no_operation;

public function {t} void
shrink_voxel_value(bool for_mipmap, t const& spri, t const& ssec,
  texel_value mutable& dpri, texel_value mutable& dsec, ivec3 const& p)
{
  uint count_ref;
  uint count_filled;
  uint count_plane;
  uint count_sphere;
  uint count_color;
  uint count_color_emit;
  plane_param pp;
  sphere_param sp;
  color_param cp;
  color_param cp_emit;
  const x0 = p.x;
  const y0 = p.y;
  const z0 = p.z;
  for (const z: 0 .. 2) {
    for (const y: 0 .. 2) {
      for (const x: 0 .. 2) {
        const offset = make_ivec3(x * 2 - 1, y * 2 - 1, z * 2 - 1);
        const v = spri.get(ui(x0 * 2 + x), ui(y0 * 2 + y), ui(z0 * 2 + z));
        const vs = ssec.get(ui(x0 * 2 + x), ui(y0 * 2 + y), ui(z0 * 2 + z));
        if (is_space_voxel(v)) {
          continue; // 色決定の分母に含めない
        } else if (is_node_reference(v)) {
          ++count_ref;
        } else if (is_filled(v)) {
          ++count_filled;
        } else if (is_plane_node(v)) {
          mutable p = decode_plane_node(v);
          // debug_log("p", p);
          p.dist += dot_int(p.nor, offset);
          p.dist /= 2;
          ++count_plane;
          // debug_log("mod p", p);
          pp = p;
        } else if (is_sphere_node(v)) {
          mutable p = decode_sphere_node(v);
          p.center += map{ivec3, operator::mul}(offset, p.scale) / 2;
          p.center /= 2;
          p.dist /= 2;
          ++count_sphere;
          sp = p;
        }
        ++count_color;
        const cpc = decode_color_param(vs);
        cp += cpc;
        if (cpc.emission != 0) {
            color_param cev;
          if (is_filled(v)) {
            cev = decode_color_param(v & 0x00ffffffu);
            /* 
            if (cev.rgb.r != 0) {
              debug_log("cev check filled", cev, to_hexstring(v));
            }
            */
          } else {
            cev = decode_color_param(vs & 0x00ffffffu);
            /*
            if (cev.rgb.r != 0) {
              debug_log("cev check notfilled", cev, to_hexstring(vs));
            }
            */
          }
          cp_emit += cev;
          ++count_color_emit;
        }
      }
    }
  }
  uint nv;
  if (for_mipmap) {
    nv = make_filled_voxel();
  } else if (count_plane > 0) {
    nv = make_plane_node(pp, ivec3(), 1);
  } else if (count_sphere > 0) {
    sp /= count_sphere;
    nv = make_sphere_node(sp, ivec3(), 1, 0u);
  } else {
    nv = make_filled_voxel();
  }
  // debug_log("set", x0, y0, z0);
  if (count_color > 0) {
    cp /= count_color;
    uint nc = encode_color_param(cp);
    if (count_color_emit > 0) {
      nv = make_filled_voxel();
      cp_emit /= 6u; // 8u;
      uint nc_emit = encode_color_param(cp_emit);
      nv |= (nc_emit & 0x00ffffffu);
      /*
      if ((nv & 0x0000ffffu) != 0) { // 青以外のemit色があったらログ出力
        debug_log("nv", to_hexstring(nv), cp_emit, count_color_emit); // FIXME
      }
      */
    }
    dpri = nv;
    dsec = nc;
  } else {
    dpri = 0u;
    dsec = 0u;
  }
}

/*
// TODO
public function {t, f} void
extract_noderef(texel_value v, t mutable& tex_pri, t mutable& tex_sec,
  uint tile_size_log2)
{
  if (!is_node_reference(v)) {
    return;
  }
  const p = decode_sphere_node(v);
  if (p.scale + 1 > tile_size_log2) {
    return;
  }
}
*/

public function {t, f} bool
scale_voxel_value(texel_value v_pri, texel_value v_sec, t mutable& tex_pri,
  t mutable& tex_sec, int x0, int y0, int z0, uint tile_size_log2)
{
  for (const z: 0 .. 2) {
    for (const y: 0 .. 2) {
      for (const x: 0 .. 2) {
        mutable cv = v_pri;
        if (is_plane_node(v_pri)) {
          mutable p = decode_plane_node(v_pri);
          p.dist = p.dist * 2;
          cv = make_plane_node(p, make_ivec3(
            x * 2 - 1,
            y * 2 - 1,
            z * 2 - 1),
            1);
        } else if (is_sphere_node(v_pri)) {
          mutable p = decode_sphere_node(v_pri);
          p.dist = p.dist * 2;
          p.center = p.center * 2;
          cv = make_sphere_node(p, make_ivec3(
            x * 2 - 1,
            y * 2 - 1,
            z * 2 - 1),
            1, 0u);
        } else if (is_node_reference(v_pri)) {
          mutable p = decode_noderef_param(v_pri);
          if (p.scale > 0) {
            p.scale -= 1;
            mutable c = to_ivec3(p.coord) * 2;
            mutable offset = make_ivec3(
              x * 2 - 1,
              y * 2 - 1,
              z * 2 - 1);
            offset = p.get_imat3() * offset;
            c = (c * 2 + offset + 1) / 2;
            p.coord = to_uvec3(c);
            if (lt_all_uvec3(p.coord, make_uvec3(32, 32, 32))) {
              cv = encode_noderef_param(p);
            } else {
              // パターン番号がエンコード可能な範囲を超えている
              return false;
            }
          } else { // p.scale == 0
            const m = p.get_imat3();
            // TODO: glmのtranspose使う
            const& s = m.to_cslice();
            const m_trans = make_imat3(
              s[0], s[3], s[6],
              s[1], s[4], s[7],
              s[2], s[5], s[8]);
            mutable offset = make_ivec3(
              x * 2 - 1,
              y * 2 - 1,
              z * 2 - 1);
            offset = m * offset;
            offset = (offset + 1) / 2;
            const c = p.coord * 2u + to_uvec3(offset);
            dbg_scale() function void () {
              debug_log("offset", offset, "c", c);
            }
            cv = f(c, texture_value_index_primary, 0u);
            cv = rotate_voxel_value(m_trans, cv);
            v_sec = f(c, texture_value_index_secondary, 0u);
          }
        }
        tex_pri.set(ui(x0 + x), ui(y0 + y), ui(z0 + z), cv);
        tex_sec.set(ui(x0 + x), ui(y0 + y), ui(z0 + z), v_sec);
      }
    }
  }
  return true;
}

public function {t} void
rotate_texture_range(imat3 const& m, t const& spri, t const& ssec,
  uvec3 const& sz, t mutable& dpri, t mutable& dsec)
{
  for (const z: 0 .. sz.z) {
    for (const y: 0 .. sz.y) {
      for (const x: 0 .. sz.x) {
	const vp = rotate_voxel_value(m, spri.get(x, y, z));
	const va = ssec.get(x, y, z);
	const p = rotate_offset(m, glm::make_ivec3(si(x), si(y), si(z)), sz);
	dbg_rotate() function void () {
	  debug_log("rotate_range", x, y, z, p, vp, va);
	}
	const up = to_uvec3(p);
	if (lt_all_uvec3(up, sz)) {
	  dpri.set(ui(p.x), ui(p.y), ui(p.z), vp);
	  dsec.set(ui(p.x), ui(p.y), ui(p.z), va);
	}
      }
    }
  }
}

public function ivec3
rotate_offset(imat3 const& m, ivec3 const& v, uvec3 const& sz)
{
  const isz = to_ivec3(sz) - glm::make_ivec3(1, 1, 1);
  const vs = v * 2 - isz;
  const vm = m * vs + isz;
  const r = map{glm::ivec3, operator::div}(vm, glm::make_ivec3(2, 2, 2));
  // debug_log("rotate", sz, v, r);
  return r;
}

public function uint
rotate_voxel_value(imat3 const& m, uint v)
{
  dbg_rotate() function void () {
    debug_log("rotate_voxel_value", to_hexstring(v), is_node_reference(v));
  }
  if (is_plane_node(v)) {
    mutable pp = decode_plane_node(v);
    pp.nor = m * pp.nor;
    v = encode_plane_node(pp);
  } else if (is_sphere_node(v)) {
    mutable sp = decode_sphere_node(v);
    sp.center = m * sp.center;
    sp.scale = map{ivec3, abs}(m * sp.scale);
    v = encode_sphere_node(sp);
  } else if (is_node_reference(v)) {
    mutable rp = decode_noderef_param(v);
    dbg_rotate() function void () {
      debug_log("old rp:", rp, "mat:", rp.get_imat3().to_cslice());
      debug_log("rot mat:", m.to_cslice());
    }
    const rm = rp.get_imat3();
      // これはシェーダがtmatの座標からtpatの座標に変換する際に適用する変換
      // なので、表示の際にはtpatデータに対してこれの逆変換が適用されたパタン
      // が表示されることになる
    const& s = m.to_cslice();
    const m_trans = make_imat3(
      s[0], s[3], s[6],
      s[1], s[4], s[7],
      s[2], s[5], s[8]);
    rp.set_imat3(rm * m_trans);
    dbg_rotate() function void () {
      debug_log("mod rp:", rp, "mat:", rp.get_imat3().to_cslice());
    }
    v = encode_noderef_param(rp);
  } else if (is_space_voxel(v)) {
    v = 0u;
  }
  return v;
}

