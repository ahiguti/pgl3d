private threaded namespace pgl3d::edit_mode::edit_mode_util "use-unsafe";
public import common -;
public import meta m;
public import pgl3d::texture::texture -;
public import pgl3d::texture::voxel -;
public import pgl3d::texture::copy_data -;
public import pgl3d::edit_mode::tiled_texture_edit_view -;
private import pgl3d::edit_mode::base -;
private import pgl3d::edit_mode::vecutil -;
private import pgl3d::edit_mode::procedural -;

private metafunction dbg_conv{f} f;

public valuetype struct tpat_data {
  public uint scale;
  public uvec3 coord;
  public farray{texture_data, 2} data;
}

public valuetype struct pattern_save_data {
  public copy_data_type copy_data;
  public varray{tpat_data} patterns;
}

/*
public function void
dump_value(tiled_texture_edit_view mutable& ttev, uvec3 const& pos)
{
  const v = ttev.tmap_edit_get(pos, texture_value_index_primary, 0);
  if (is_node_reference(v)) {
    const nrp = decode_noderef_param(v);
    debug_log("dump: noderef", nrp);
  }
}
*/

public function void
erase_unused_tpat(tiled_texture_edit_view mutable& ttev)
{
  uint const scale_log2 = 4u;
  uvec3 const tpat_size = ttev.tpat_size;
  uint const tile_size = 1u << scale_log2;
  uvec3 const tile_size3 = make_uvec3(1u, 1u, 1u) * tile_size;
  uvec3 const coord_range = tpat_size / tile_size;
  texture_data em;
  texture_data um;
  uvec3 const tsl2 = map{uvec3, integral_log2{uint}}(tpat_size);
  em.set_size_l2(tsl2.x - scale_log2, tsl2.y - scale_log2,
    tsl2.z - scale_log2);
  um.set_size_l2(tsl2.x - scale_log2, tsl2.y - scale_log2,
    tsl2.z - scale_log2);
  // tpatのうち空白パターンに印をつける
  range_foreach(coord_range) function void (uint x, uint y, uint z) {
    const c = make_uvec3(x, y, z);
    const pos = c * tile_size;
    farray{texture_data, 2} tex;
    tex[0].set_size_l2(scale_log2, scale_log2, scale_log2);
    tex[1].set_size_l2(scale_log2, scale_log2, scale_log2);
    ttev.tpat_edit_copy_range(pos, tile_size3, tex[0], tex[1], uvec3());
    bool is_space = true;
    tex[0].foreach() function void (uint x, uint y, uint z, uint v) {
      if (!is_space_voxel(v)) {
        is_space = false;
      }
    }
    if (is_space) {
      em.set(x, y, z, 1u);
    }
  }
  // tmapを見て使用しているパターンに印をつける
  range_foreach(ttev.tmap_size) function void (uint x, uint y, uint z) {
    const v = ttev.tmap_edit_get(make_uvec3(x, y, z),
      texture_value_index_primary, 0);
    if (!is_node_reference(v)) {
      return;
    }
    mutable nrp = decode_noderef_param(v);
    uvec3 p = nrp.coord;
    const s = nrp.scale + 1u;
    if (s <= scale_log2) {
      // scaleが基準サイズ以下のとき。
      const sv = scale_log2 - s;
      p.x >>= sv;
      p.y >>= sv;
      p.z >>= sv;
      if (em.get(p.x, p.y, p.z) != 0u) {
        // 空パターンを指している。tmapのエントリを空白に置き換える。
        debug_log("empty noderef", x, y, z);
        ttev.tmap_edit_set(make_uvec3(x, y, z), texture_value_index_primary,
          0u);
        ttev.tmap_edit_set(make_uvec3(x, y, z), texture_value_index_secondary,
          0u);
      } else {
        // 使用中の印をつける
        um.set(p.x, p.y, p.z, 1u);
      }
    } else {
      // scaleが基準サイズより大きいとき。
      const sv = s - scale_log2;
      p.x <<= sv;
      p.y <<= sv;
      p.z <<= sv;
      uint sz = 1u << sv;
      for (uint z: 0 .. sz) {
        for (uint y: 0 .. sz) {
          for (uint x: 0 .. sz) {
            // 使用中の印をつける
            um.set(p.x + x, p.y + y, p.z + z, 1u);
          }
        }
      }
    }
  }
  // 使用していないパターンを空白にする。
  {
    farray{texture_data, 2} tex;
    tex[0].set_size_l2(scale_log2, scale_log2, scale_log2);
    tex[1].set_size_l2(scale_log2, scale_log2, scale_log2);
    uint cnt_use = 0;
    uint cnt_empty = 0;
    uint cnt_clear = 0;
    range_foreach(coord_range) function void (uint x, uint y, uint z) {
      if (um.get(x, y, z) != 0u) {
        ++cnt_use;
      } else if (em.get(x, y, z) != 0u) {
        ++cnt_empty;
      } else {
        ++cnt_clear;
        const pos = make_uvec3(x, y, z) * tile_size;
        debug_log("clear tpat offset", make_uvec3(x, y, z));
        ttev.tpat_edit_paste_range(pos, tile_size3, tex[0], tex[1], uvec3(),
          0xffu, merge_mode_opaque());
      }
    }
    debug_log("erase_unused_tpat use=", cnt_use, "empty=", cnt_empty,
      "clear=", cnt_clear);
  }
}

public function void
dedup_tpat(tiled_texture_edit_view mutable& ttev)
{
  // 重複するタイルパターンを一つにまとめる。16^3サイズのみサポート。
  uint const scale_log2 = 4u;
  uvec3 const tpat_size = ttev.tpat_size;
  uint const tile_size = 1u << scale_log2;
  uvec3 const tile_size3 = make_uvec3(1u, 1u, 1u) * tile_size;
  uvec3 const coord_range = tpat_size / tile_size;
  tree_map{varray{uint}, uvec3} val_to_coord;
  tree_map{uvec3, uvec3} coord_map;
  // 重複を見つける
  range_foreach(coord_range) function void (uint x, uint y, uint z) {
    const c = make_uvec3(x, y, z);
    const pos = c * tile_size;
    farray{texture_data, 2} tex;
    tex[0].set_size_l2(scale_log2, scale_log2, scale_log2);
    tex[1].set_size_l2(scale_log2, scale_log2, scale_log2);
    ttev.tpat_edit_copy_range(pos, tile_size3, tex[0], tex[1], uvec3());
    varray{uint} k;
    // primaryの値をkに追加
    tex[0].foreach() function void (uint x, uint y, uint z, uint v) {
      if (is_space_voxel(v)) {
        // 空白距離情報を削除
        v = make_space_voxel();
      }
      k.push_back(v);
    }
    bool is_empty = true;
    for (const i, const v: k) {
      if (!is_space_voxel(v)) {
        is_empty = false;
      }
    }
    if (is_empty) {
      // 全てspaceなら何もしない
      return;
    }
    // secondaryの値をkに追加
    tex[1].foreach() function void (uint x, uint y, uint z, uint v) {
      k.push_back(v);
    }
    if (const e: val_to_coord[k]) {
      debug_log("found dup", c, e, k);
      coord_map[c] = e;
    } else {
      val_to_coord.insert(k, c);
      // debug_log("insert pat", c, k);
    }
  }
  debug_log("num patterns", val_to_coord.size());
  debug_log("num dups", coord_map.size());
  if (coord_map.empty()) {
    return;
  }
  // タイル番号を置換
  range_foreach(ttev.tmap_size) function void (uint x, uint y, uint z) {
    const v = ttev.tmap_edit_get(make_uvec3(x, y, z),
      texture_value_index_primary, 0);
    if (!is_node_reference(v)) {
      return;
    }
    mutable nrp = decode_noderef_param(v);
    if (nrp.scale != scale_log2 - 1u) {
      return;
    }
    if (const dst: coord_map[nrp.coord]) {
      nrp.coord = dst;
      const nv = encode_noderef_param(nrp);
      ttev.tmap_edit_set(make_uvec3(x, y, z), texture_value_index_primary,
        nv);
      debug_log("replace noderef", x, y, z);
    }
  }
  debug_log("replace done", coord_map.size());
  // 重複タイルを空白で埋める
  {
    farray{texture_data, 2} tex;
    tex[0].set_size_l2(scale_log2, scale_log2, scale_log2);
    tex[1].set_size_l2(scale_log2, scale_log2, scale_log2);
    for (const& src, const& dst: coord_map) {
      debug_log("fill", src);
      const pos = src * tile_size;
      ttev.tpat_edit_paste_range(pos, tile_size3, tex[0], tex[1], uvec3(),
        0xffu, merge_mode_opaque());
    }
  }
  debug_log("dedup done");
}

public function texel_value
find_empty_tpat(tiled_texture_edit_view mutable& ttev,
  uvec3 mutable& find_empty_cur, uint cur_tile_scale_log2,
  uvec3 blksz)
{
  const p0 = find_empty_cur;
  mutable p = p0;
  const tsz = 1u << cur_tile_scale_log2;
  const tpsz = ttev.tpat_size / tsz;
  while (true) {
    if (++p.x >= tpsz.x) {
      p.x = 0;
      if (++p.y >= tpsz.y) {
	p.y = 0;
	if (++p.z >= tpsz.z) {
	  p.z = 0;
	}
      }
    }
    if (p == p0) {
      break;
    }
    if (!le_all_uvec3(p + blksz, tpsz)) {
      continue;
    }
    function bool is_space_pat(uvec3 p) {
      const pbase = p * tsz;
      bool r = true;
      for (const z: 0 .. tsz) {
	for (const y: 0 .. tsz) {
	  for (const x: 0 .. tsz) {
	    const cp = make_uvec3(pbase.x + x, pbase.y + y, pbase.z + z);
	    const cv = ttev.tpat_edit_get(cp, texture_value_index_primary,
	      0u);
	    r &= is_space_voxel(cv);
	  }
	}
      }
      return r;
    }
    bool is_space = true;
    range_foreach(blksz) function void (uint x, uint y, uint z) {
      is_space &= is_space_pat(p + make_uvec3(x, y, z));
    }
    dbg_conv() function void () {
      debug_log("find_empty_tpat", p, is_space);
    }
    if (is_space) {
      find_empty_cur = p;
      noderef_param np;
      np.coord = p;
      np.scale = cur_tile_scale_log2 - 1u;
      return encode_noderef_param(np);
    }
  }
  return make_space_voxel();
}

public function bool
convert_from_pattern_save_data(pattern_save_data const& psd,
  copy_data_type mutable& cd, tiled_texture_edit_view mutable& ttev)
{
  function {t} uint pat_to_uint(t const& nd) {
    const& c = nd.coord;
    uint r = (nd.scale << 24u) | (c.z << 16u) | (c.y << 8u) | (c.x << 0u);
    return r;
  }
  function {t} t uint_to_pat(uint v) {
    t nd;
    nd.scale = (v >> 24u) & 0xffu;
    nd.coord.z = (v >> 16u) & 0xffu;
    nd.coord.y = (v >>  8u) & 0xffu;
    nd.coord.x = (v >>  0u) & 0xffu;
    return nd;
  }
  bool err = false;
  cd = psd.copy_data;
  uvec3 fecur;
  uint cur_tile_scale_log2;
  tree_map{uint, uint} patconv;
  for (const i, const& pat: psd.patterns) {
    if (cur_tile_scale_log2 != pat.scale + 1u) {
      cur_tile_scale_log2 = pat.scale + 1u;
      fecur = uvec3();
    }
    const tv = find_empty_tpat(ttev, fecur, cur_tile_scale_log2,
      make_uvec3(1u, 1u, 1u));
    if (!is_node_reference(tv)) {
      err = true;
      break;
    }
    const nrp = decode_noderef_param(tv);
    patconv[pat_to_uint(pat)] = pat_to_uint(nrp);
    const pos = nrp.coord * (1u << (pat.scale + 1u));
    const sz = make_uvec3(1u, 1u, 1u) * (1u << (pat.scale + 1u));
    ttev.tpat_edit_paste_range(pos, sz, pat.data[0], pat.data[1], uvec3(),
      0xffu, merge_mode_opaque());
  }
  if (err) {
    cd = copy_data_type();
    return false;
  }
  mutable& tex_pri = cd.tex[0];
  range_foreach(cd.copy_size) function void (uint x, uint y, uint z) {
    const v = tex_pri.get(x, y, z);
    if (!is_node_reference(v)) {
      return;
    }
    mutable nr = decode_noderef_param(v);
    if (const p: patconv[pat_to_uint(nr)]) {
      const cnr = uint_to_pat{noderef_param}(p);
      nr.coord = cnr.coord;
      tex_pri.set(x, y, z, encode_noderef_param(nr));
    } else {
      debug_log("failed to convert noderef (internal error)");
      err = true;
    }
  }
  return !err;
}

public function bool
convert_to_pattern_save_data(pattern_save_data mutable& psd,
  copy_data_type const& cd, tiled_texture_edit_view mutable& ttev)
{
  psd = pattern_save_data();
  psd.copy_data = cd;
  if (!get_tpat_refs(psd.patterns, cd, ttev)) {
    return false;
  }
  return true;
}

private function bool
get_tpat_refs(varray{tpat_data} mutable& tpd, copy_data_type const& cd,
  tiled_texture_edit_view mutable& ttev)
{
  /* 現状16倍以外はサポートしていない。それ以外のscaleでのsave/loadは
   * いったん16倍でおこなうこと。*/
  const scale_val = 4u - 1u;
  bool err = false;
  tree_set{uvec3} refs;
  const sz = cd.copy_size;
  for (const z: 0 .. sz.z) {
    for (const y: 0 .. sz.y) {
      for (const x: 0 .. sz.x) {
        const v = cd.tex[0].get(x, y, z);
        if (!is_node_reference(v)) {
          continue;
        }
        const nrp = decode_noderef_param(v);
        if (nrp.scale != scale_val) {
          err = true;
          debug_log("not supported", nrp);
          continue;
        }
        refs[nrp.coord] = unit();
      }
    }
  }
  for (const c, const u: refs) {
    const scale_val_p1 = scale_val + 1u;
    const scale = 1u << scale_val_p1;
    const pos = c * scale;
    const sz = make_uvec3(scale, scale, scale);
    debug_log("coord", c, "scale", scale, "sz", sz);
    tpat_data v;
    v.scale = scale_val;
    v.coord = c;
    v.data[0].set_size_l2(scale_val_p1, scale_val_p1, scale_val_p1);
    v.data[1].set_size_l2(scale_val_p1, scale_val_p1, scale_val_p1);
    ttev.tpat_edit_copy_range(pos, sz, v.data[0], v.data[1], uvec3());
    tpd.push_back_move(v);
  }
  return !err;
}

