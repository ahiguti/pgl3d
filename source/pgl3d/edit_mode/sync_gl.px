private threaded namespace pgl3d::edit_mode::sync_gl "use-unsafe";
public import core::common -;
public import core::meta m;
private import pgl3d::edit_mode::common -;
private import pgl3d::edit_mode::vecutil -;
private import pgl3d::edit_mode::edit_mode_texture -;

public threaded function {t} void
texture_data_sync_gl(t const& tex, int level, uvec3 const& spos,
  uvec3 const& sz, ptr{gl_texture} const& gltex)
{
  mutable buf = darray{uint}(static_cast{size_t}(sz.x * sz.y * sz.z), 0u);
    /* TODO: 毎回ヒープ割り当てするのをやめる */
  uint i;
  for (const z: 0 .. sz.z) {
    for (const y: 0 .. sz.y) {
      for (const x: 0 .. sz.x) {
        /*
        if (spos.x + x >= tex.get_size_uvec3().x ||
            spos.y + y >= tex.get_size_uvec3().y ||
            spos.z + z >= tex.get_size_uvec3().z) {
          debug_log("texture_data_sync_gl invalid index",
            tex.get_size_uvec3(), spos, x, y, z);
        }
        */
        buf[i] = tex.get(spos.x + x, spos.y + y, spos.z + z);
        ++i;
      }
    }
  }
  // debug_log("TexSubImage3D", sz.x, sz.y, sz.z);
  glActiveTexture(GL_TEXTURE0);
  glBindTexture(GL_TEXTURE_3D, gltex->texture_id.get());
  glTexSubImage3D(GL_TEXTURE_3D, level, si(spos.x), si(spos.y), si(spos.z),
    si(sz.x), si(sz.y), si(sz.z), GL_RGBA, GL_UNSIGNED_BYTE,
    buf.to_crawptr().to_cvoidptr());
}

public threaded function void
texture_data_mip_sync_gl(texture_reshaped const& tex0,
  texture_data_mip const& mip, uvec3 const& spos, uvec3 const& sz,
  ptr{gl_texture} const& gltex)
{
  /* miplevel0だけtex0、残りはmip[i]をgltextureに書き込む */
  uvec3 pos_a = spos;
  uvec3 pos_b = spos + sz;
  for (const i, const& tex: mip) {
    uvec3 const msz = pos_b - pos_a;
    // debug_log("mip", i, pos_a, pos_b, msz);
    if (i == 0) {
      texture_data_sync_gl(tex0, si(i), pos_a, msz, gltex);
    } else {
      texture_data_sync_gl(tex, si(i), pos_a, msz, gltex);
    }
    pos_b = pos_b + make_uvec3(1u, 1u, 1u);
    pos_a = map{uvec3, core::operator::div}(pos_a, make_uvec3(2u, 2u, 2u));
    pos_b = map{uvec3, core::operator::div}(pos_b, make_uvec3(2u, 2u, 2u));
  }
}

public threaded function void
sync_gl_mipmap_all(edit_mode_texture mutable& emt)
{
  sync_gl_miplevels(emt, 1);
}

public threaded function void
sync_gl_all(edit_mode_texture mutable& emt)
{
  sync_gl_miplevels(emt, 0);
}

private threaded function void
sync_gl_miplevels(edit_mode_texture mutable& emt, size_t lvmin)
{
  for (const i: lvmin .. emt.vox.tpat_edit->data[0].size()) {
    for (const j, mutable& dataj: emt.vox.tpat_edit->data) { 
      const sz0 = dataj[i].get_size_uvec3();
      const sz1 = emt.vox.tpat_edit->data[j][i].get_size_uvec3();
      debug_log("sync_gl_mipmap_all tpat", i, j, sz0, sz1);
      if (j >= 2) {
        throw runtime_error_template{"sync_gl_miplevels"}("tpat");
      }
      ptr{gl_texture} tex = (j == 0) ? emt.glt_voxtpat0 : emt.glt_voxtpat1;
      texture_data_sync_gl(dataj[i], si(i), uvec3(),
        emt.vox.tpat_edit->data[j][i].get_size_uvec3(), tex);
    }
  }
  for (const i: lvmin .. emt.vox.tmap_edit->data[0].size()) {
    for (const j, mutable& dataj: emt.vox.tmap_edit->data) {
      const sz0 = dataj[i].get_size_uvec3();
      const sz1 = emt.vox.tpat_edit->data[j][i].get_size_uvec3();
      if (j >= 2) {
        throw runtime_error_template{"sync_gl_miplevels"}("tmap");
      }
      debug_log("sync_gl_mipmap_all tmap", i, j, sz0, sz1);
      ptr{gl_texture} tex = (j == 0) ? emt.glt_voxtmap0 : emt.glt_voxtmap1;
      texture_data_sync_gl(dataj[i], si(i), uvec3(),
        emt.vox.tmap_edit->data[j][i].get_size_uvec3(), tex);
    }
  }
}

