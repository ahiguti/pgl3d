private threaded namespace pgl3d::opengl::glresource "export-unsafe";
public import common -;
public import glm;
public import opengl::compat -;
public import meta m;
public import meta::vararg v;
public import exception -;
public import pointer::raw -;
public import sdl2::api_base *;
public import sdl2::api_types *;
public import sdl2::api_functions *;
public import sdl2::api_wrappers *;
public import sdl2::misc *;
public import pgl3d::engine::platform *;

public threaded struct extern "pgl3d$n::glresource$n::handle" "noncopyable" {t}
handle()
{
  public t value;
}

extern "types" inline
namespace pgl3d$n {
namespace glresource$n {
template <typename T> struct handle
{
  handle() : value(T()) { }
  handle(handle&& v) : value(T()) { std::swap(value, v.value); }
  handle(handle& v) : value(T()) { std::swap(value, v.value); }
  handle& operator =(handle&& v) { std::swap(value, v.value); return *this; }
  handle& operator =(handle& v) { std::swap(value, v.value); return *this; }
  T value;
private:
  handle(handle const&) = delete;
  handle& operator =(handle const&) = delete;
};
};
};
;

public metafunction auto_buffer
  auto_handle{uint, glGenBuffers, glDeleteBuffers};
public metafunction auto_texture
  auto_handle{uint, glGenTextures, glDeleteTextures};
public metafunction auto_framebuffer
  auto_handle{uint, glGenFramebuffers, glDeleteFramebuffers};
public metafunction auto_renderbuffer
  auto_handle{uint, glGenRenderbuffers, glDeleteRenderbuffers};
expand (sym: m::seq{is_gl3_or_gles3}) {
  public metafunction auto_vertexarray
    auto_handle{uint, glGenVertexArrays, glDeleteVertexArrays};
}
expand (sym: m::seq{m::not{is_gl3_or_gles3}}) {
  public metafunction auto_vertexarray
    auto_handle{uint, 0, 0};
}

private threaded struct {t, generate_func, release_func}
auto_handle()
{
  private handle{t} hnd = handle{t}();
  public function t get() const
  {
    return hnd.value;
  }
  public function void generate()
  {
    reset();
    if (m::ne{generate_func, 0}) {
      generate_func(1, address(hnd.value));
    }
  }
  public function void generate_if()
  {
    if (hnd.value > 0) {
      return;
    }
    generate();
  }
  public function void reset()
  {
    if (hnd.value > 0) {
      if (m::ne{release_func, 0}) {
	release_func(1, caddress(hnd.value));
      }
      hnd.value = 0;
    }
  }
  private function ~ {
    reset();
  }
}

public metafunction num_float_struct{t}
  m::fold{m::map{m::field_types{t}, num_float_fld}, m::add, 0};

public metafunction num_float_fld{t}
  m::cond{
    m::eq{t, float}, 1,
    m::eq{t, int}, 1,
    m::eq{t, unit}, 0,
    m::eq{t, glm::vec2}, 2,
    m::eq{t, glm::vec3}, 3,
    m::eq{t, glm::vec4}, 4,
    m::eq{t, glm::mat2}, 4,
    m::eq{t, glm::mat3}, 9,
    m::eq{t, glm::mat4}, 16,
    m::eq{m::family{t}, "farray"},
      m::mul{num_float_fld{m::at0{t}}, m::at1{t}},
    m::error{"invalid type"}};

public metafunction num_attrib_fld{t}
  m::cond{
    m::eq{t, float}, 1,
    m::eq{t, int}, 1,
    m::eq{t, unit}, 0,
    m::eq{t, glm::vec2}, 1,
    m::eq{t, glm::vec3}, 1,
    m::eq{t, glm::vec4}, 1,
    m::eq{t, glm::mat2}, 2,
    m::eq{t, glm::mat3}, 3,
    m::eq{t, glm::mat4}, 4,
    m::error{"invalid type"}};

